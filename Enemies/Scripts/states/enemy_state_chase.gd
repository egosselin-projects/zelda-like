class_name EnemyStateChase extends EnemyState

@export var animation_name: String = "chase"
@export var chase_speed: float = 40.0
@export var turn_rate: float = 0.25

@export_category("AI")
@export var vision_area: VisionArea
@export var attack_area: HurtBox
@export var state_agro_duration: float = 0.5
@export var after_next_state: EnemyState

var _timer: float = 0.0
var _direction: Vector2
var _can_see_player: bool = false

func init() -> void:
	if vision_area:
		vision_area.player_entered.connect(_on_player_enter)
		vision_area.player_exited.connect(_on_player_exit)

func _ready() -> void:
	pass


# Entrée dans l'état
func enter_state() -> void:
	_timer = state_agro_duration

	enemy.update_animation(animation_name)

	if attack_area:
		attack_area.monitoring = true


# Sortie de l'état
func exit_state() -> void:
	if attack_area:
		attack_area.monitoring = false
	_can_see_player = false

func process(_delta: float) -> EnemyState:
	var new_direction: Vector2 = enemy.global_position.direction_to(PlayerManager.player.global_position)
	_direction = lerp(_direction, new_direction, turn_rate)
	enemy.velocity = _direction * chase_speed
	
	if enemy.set_direction(_direction):
		enemy.update_animation(animation_name)
	
	if not _can_see_player:
		_timer -= _delta
		if _timer <= 0:
			return after_next_state
	else:
		_timer = state_agro_duration

	return null


func physics(_delta: float) -> EnemyState:
	return null


func _on_player_enter() -> void:
	_can_see_player = true
	
	# Un enemi dans l'état assomé ne doit pas changer immédiatement d'état
	if state_machine.current_state is EnemyStateStun:
		return
	
	state_machine.change_state(self)

func _on_player_exit() -> void:
	_can_see_player = false
